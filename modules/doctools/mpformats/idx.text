# -*- tcl -*-
#
# $Id: idx.text,v 1.1 2003/04/01 01:20:33 andreas_kupries Exp $
#
# Engine to convert a docidx document into plain text.
#
# Copyright (c) 2003 Andreas Kupries <andreas_kupries@sourceforge.net>
# Freely redistributable.
#
######################################################################

dt_source _idx_common.tcl
dt_source _text.tcl
proc c_copyrightsymbol {} {markup "(c)"}

######################################################################
# Conversion specification.
# One-pass processing.

rename idx_postprocess {}
rename text_postprocess idx_postprocess
proc   fmt_plain_text {text} {return {}}

################################################################
## Backend for plain text markup

global map ; array set map {}
global key ; set key {}
global max ; set max 0

proc fmt_index_begin {label title} {
    global map ; unset map ; array set map {}
    global key ; set key {}
    global max ; set max 0

    set hdr ""
    if {($label != {}) && ($title != {})} {
	append  hdr "$label -- $title"
    } elseif {$label != {}} {
	append  hdr $label
    } elseif {$title != {}} {
	append  hdr $title
    }
    append hdr [c_provenance]\n
    append hdr \n\n

    Text $hdr
    CloseParagraph [Verbatim]
    return
}
proc fmt_index_end {} {
    global map

    set break 0
    set rmargin [expr {80 - $max}]
    if {$rmargin < 20} {set rmargin 20}
    incr max
    set pfx [textutil::blank $max]

    foreach key [lsort [array names map]] {
	set   opfx $key[string range $pfx [string length $key] end]
	Text $opfx[textutil::indent [textutil::adjust [join $map($key) ", "] -length $rmargin] $pfx 1]
	CloseParagraph [Verbatim]
    }
    return
}
proc fmt_key {text} {
    global key max ; set key $text
    if {[string length $text] > $max} {set max [string length $text]}
    return
}
proc fmt_manpage {file label} {global map key ; lappend map($key) $file ; return}
proc fmt_url     {url label}  {global map key ; lappend map($key) $url ; return}
proc fmt_comment {text}       {return}

################################################################
