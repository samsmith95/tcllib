# interpolate.test --
#    Test cases for the ::math::interpolate package
#

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::test
    namespace import ::tcltest::customMatch
}

source [file join [file dirname [info script]] math.tcl]
source [file join [file dirname [info script]] interpolate.tcl]

package require math
package require struct

#
# Minimisation via steepest-descent
#
proc matchNumbers {expected actual} {
    set match 1
    foreach a $actual e $expected {
        if {$e != 0.0} {
            if {abs($a-$e) > 0.5e-4*abs($a+$e)} {
                set match 0
                break
            }
        } else {
            if {abs($a-$e) > 1.0e-5} {
                set match 0
                break
            }
        }
    }
    return $match
}

customMatch numbers matchNumbers

#
# Test cases: interpolation in tables
#
set t [::math::interpolate::defineTable table1 \
   {    x     v1    v2    v3 } \
   {    0      0    10     1
        1      1     9     4
        2      2     8     9
        5      5     5    25
        7      7     3    49
       10     10     0   100 }]

test "Interpolate-1.1" "Interpolate in a one-dimensional table" \
     -match numbers -body {
   set result {}
   foreach x { -1.0 0.0 3.0 5.0 9.9 11.0 } {
      set result [concat $result \
                 [::math::interpolate::interp-1d-table $t $x]]
   }
   set result
} -result {
   -1    0    10     1
    0    0    10     1
    3    3     7    14.333333
    5    5     5    25
  9.9  9.9     0.1  98.3
   11   10     0   100 }


# value = x+y
set t2 [::math::interpolate::defineTable table2 \
   {    x      y1   y2    y3 } \
   {    -      0     3    10
        1      1     4    11
        2      2     5    12
        5      5     8    15
        7      7    10    17
       10     10    13    20 }]

test "Interpolate-1.2" "Interpolate in a two-dimensional table" \
     -match numbers -body {
   set result {}
   foreach y { -1.0 0.0 3.0 5.0 9.9 11.0 } {
      foreach x { -1.0 0.0 3.0 5.0 9.9 11.0 } {
         set result [concat $result \
            $x $y [::math::interpolate::interp-table $t2 $x $y]]
      }
   }
   set result
} -result {
    -1.0 -1.0 1.0
     0.0 -1.0 1.0
     3.0 -1.0 3.0
     5.0 -1.0 5.0
     9.9 -1.0 9.9
    11.0 -1.0 10.0
    -1.0 0.0 1.0
     0.0 0.0 1.0
     3.0 0.0 3.0
     5.0 0.0 5.0
     9.9 0.0 9.9
    11.0 0.0 10.0
    -1.0 3.0 4.0
     0.0 3.0 4.0
     3.0 3.0 6.0
     5.0 3.0 8.0
     9.9 3.0 12.9
    11.0 3.0 13.0
    -1.0 5.0 6.0
     0.0 5.0 6.0
     3.0 5.0 8.0
     5.0 5.0 10.0
     9.9 5.0 14.9
    11.0 5.0 15.0
    -1.0 9.9 10.9
     0.0 9.9 10.9
     3.0 9.9 12.9
     5.0 9.9 14.9
     9.9 9.9 19.8
    11.0 9.9 19.9
    -1.0 11.0 11.0
     0.0 11.0 11.0
     3.0 11.0 13.0
     5.0 11.0 15.0
     9.9 11.0 19.9
    11.0 11.0 20.0
}

# linear interpolation: y = x + 1 and y = 2*x, x<5, or 20-2*x, x>5

test "Interpolate-2.1" "Linear interpolation - 1" \
     -match numbers -body {
   set result {}

   set xyvalues { 0.0 1.0  10.0 11.0 }
   foreach x { 0.0 4.0 7.0 10.0 101.0 } {
      lappend result [::math::interpolate::interp-linear $xyvalues $x]
   }
   set result
} -result { 1.0 5.0 8.0 11.0 11.0 }

test "Interpolate-2.2" "Linear interpolation - 2" \
     -match numbers -body {
   set result {}
   set xyvalues { 0.0 0.0  5.0 10.0 10.0 0.0 }
   foreach x { 0.0 4.0 7.0 10.0 11.0 } {
      lappend result [::math::interpolate::interp-linear $xyvalues $x]
   }
   set result
} -result { 0.0 8.0 6.0 0.0 0.0 }

# Lagrange interpolation: y = x + 1
test "Interpolate-3.1" "Lagrange interpolation - 1" \
     -match numbers -body {
   set result {}
   set xyvalues { 0.0 1.0  10.0 11.0 }
   foreach x { 0.0 4.0 7.0 10.0 101.0 } {
      lappend result [::math::interpolate::interp-lagrange $xyvalues $x]
   }
   set result
} -result { 1.0  5.0  8.0  11.0  102.0 }


#Lagrange interpolation (2) - expected: y=10-2*(x-5)**2/5
test "Interpolate-3.2" "Lagrange interpolation - 2" \
     -match numbers -body {
   set result {}
   set xyvalues { 0.0 0.0  5.0 10.0 10.0 0.0 }
   foreach x { 0.0 4.0 7.0 10.0 11.0 } {
      lappend result [::math::interpolate::interp-lagrange $xyvalues $x]
   }
   set result
} -result { 0.0 9.6 8.4 0.0 -4.4 }

# Spatial interpolation
test "Interpolate-4.1" "Spatial interpolation - 1" \
     -match numbers -body {
   set result {}
   set xyzvalues { {-1.0 0.0 -2.0 }
                   { 1.0 0.0  2.0 } }
   foreach coord { {0.0 0.0} {0.0 1.0} {3.0 0.0} {100.0 0.0} } {
      lappend result [::math::interpolate::interp-spatial $xyzvalues $coord]
   }
   set result
} -result { 0.0 0.0 1.2 0.039996 }

test "Interpolate-4.2" "Spatial interpolation - 2" \
   -match numbers -body {
   set result {}

   set xyzvalues { {-1.0 0.0 { -2.0  1.0 } }
                   { 1.0 0.0 {  2.0 -1.0 } } }
   foreach coord { {0.0 0.0} {0.0 1.0} {3.0 0.0} {100.0 0.0} } {
      set result [concat $result \
            [::math::interpolate::interp-spatial $xyzvalues $coord]]
   }
   set result
} -result { 0.0       0.0
            0.0       0.0
            1.2      -0.6
            0.039996 -0.019998 }

#
# Test TODO: parameters for spatial interpolation
#
