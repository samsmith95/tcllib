#! /bin/sh
# -*- tcl -*- \
exec tclsh "$0" ${1+"$@"}

# @@ Meta Begin
# Application nnst 1.0
# Meta platform     tcl
# Meta summary      Nano Name Service Test Client
# Meta description  This application connects to a name service demon
# Meta description  and binds itself to a configurable name/data pair.
# Meta description  It does not exit on its own, to keep the name alive.
# Meta subject      {name service} client test
# Meta require      {Tcl 8.4}
# Meta require      comm
# Meta require      logger
# Meta require      nameserv::common
# Meta require      nameserv
# Meta author       Andreas Kupries
# Meta license      BSD
# @@ Meta End

package provide nnst 1.0

# nnst - Nano Name Service Test Client
# ==== = =============================
#
# Use cases
# ---------
# 
# (1)	Query a nano name service
#	
# Command syntax
# --------------
#
# Ad 1) nnst ?-host NAME|IP? ?-port PORT? name data
#
#       Register a name with data. If no port is specified the default
# 	port 38573 is used to connect to it. If no host is specified
# 	the default (localhost) is used to connect to it.

lappend auto_path [file join [file dirname [file dirname [file normalize [info script]]]] modules]

package require nameserv

namespace eval ::nnst {}

proc ::nnst::ProcessCommandLine {} {
    global argv
    variable xname
    variable xdata

    # Process the options, perform basic validation.

    while {[llength $argv]} {
	set opt [lindex $argv 0]
	if {![string match "-*" $opt]} break

	switch -exact -- $opt {
	    -host {
		if {[llength $argv] % 2 == 1} Usage

		set host [lindex $argv 1]
		set argv [lrange $argv 2 end]

		nameserv::configure -host $host
	    }
	    -port {
		if {[llength $argv] % 2 == 1} Usage

		# Todo: Check non-zero unsigned short integer
		set port [lindex $argv 1]
		set argv [lrange $argv 2 end]

		nameserv::configure -port $port
	    }
	    -debug {
		# Undocumented. Activate the logger services provided
		# by various packages.
		logger::setlevel debug
		set argv [lrange $argv 1 end]
	    }
	    default {
		Usage
	    }
	}
    }

    # Additional validation, and extraction of the non-option
    # arguments. Of which this application has none.

    if {[llength $argv] != 2} Usage

    foreach {xname xdata} $argv break
    return
}

proc ::nnst::Usage {} {
    global argv0
    puts stderr "$argv0 wrong#args, expected:\
	    ?-host NAME|IP? ?-port PORT? NAME DATA"
    exit 1
}

proc ::nnst::ArgError {text} {
    global argv0
    puts stderr "$argv0: $text"
    exit 1
}

# ### ### ### ######### ######### #########
## Setup a text|graphical report

proc ::nnst::blank {s} {
    regsub -all -- {[^	]} $s { } s
    return $s
}

# ### ### ### ######### ######### #########
## Main

proc ::nnst::Do {} {
    global argv0
    variable xname
    variable xdata

    ProcessCommandLine

    set mp [nameserv::protocol]
    set sp [nameserv::server_protocol]
    set sf [join [nameserv::server_features] {, }]

    if {[llength $sf] > 1} {
	set sf [linsert $sf end-1 and]
    }

    set app "$argv0 [package require nnst]"
    set pfx [blank $app]

    puts "$app (Client Protocol $mp)"
    puts "$pfx (Server Protocol $sp, Features: $sf)"

    puts "Binding name \"$xname\""
    puts " Client data \"$xdata\""

    nameserv::bind $xname $xdata
    vwait forever
    return
}

# ### ### ### ######### ######### #########
## Invoking the functionality.

if {[catch {
    ::nnst::Do
} msg]} {
    ::nnst::ArgError $msg
}

# ### ### ### ######### ######### #########
exit
